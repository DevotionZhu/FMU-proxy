buildscript {
    ext.kotlin_version = '1.2.21'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}


apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'no.mechatronics.sfi.fmu_proxy'
version = '0.1-SNAPSHOT'

repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/" //fmi-import
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {

    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    compile group: 'info.picocli', name: 'picocli', version: '2.3.0'

    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'

    compile group: 'io.grpc', name: 'grpc-netty', version: '1.10.0'

    compile group: 'info.laht', name: 'yaj-rpc', version: '0.5-SNAPSHOT', changing: true
    compile group: 'no.mechatronics.sfi.fmi4j', name: 'fmi-import', version: '0.2.8-SNAPSHOT', changing: true

    compile group: 'no.mechatronics.sfi.fmu_proxy', name: 'gen-thrift-src', version: '0.1-SNAPSHOT', changing: true
    compile group: 'no.mechatronics.sfi.fmu_proxy', name: 'gen-proto-src', version: '0.1-SNAPSHOT', changing: true

    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    runtimeOnly group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.25'

    testImplementation project(':grpc-client')
    testImplementation project(':thrift-client')

}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}


signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name archivesBaseName
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'A Java server, written in Kotlin, for exposing FMUs over the network using gRPC '
                url 'https://github.com/SFI-Mechatronics/gRPC-FMU'

                scm {
                    connection 'scm:git:git://github.com/SFI-Mechatronics/gRPC-FMU.git'
                    developerConnection 'scm:git:ssh://github.com/SFI-Mechatronics/gRPC-FMU.git'
                    url 'https://github.com/SFI-Mechatronics/gRPC-FMU/tree/master'
                }

                licenses {
                    license {
                        name 'The MIT License'
                        url 'https://opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id 'laht'
                        name 'Lars Ivar Hatledal'
                        email 'laht@ntnu.no'
                    }
                }
            }
        }
    }
}