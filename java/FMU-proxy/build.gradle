
plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.51" apply false //apply in sub-projects
}

println "Gradle version is ${GradleVersion.current().version}"

wrapper {
    gradleVersion = '4.9'
    distributionType = "all"
}

ext.fmi4j_stable_version = '0.7.4'
ext.fmi4j_snapshot_version = '0.7.5-SNAPSHOT'

ext.grpc_version = '1.13.2'
ext.yajrpc_version = '0.8'
ext.slf4j_version = '1.7.25'

subprojects { sub ->

    apply plugin: 'java'

    repositories {
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    def junit_version = '5.2.0'

    dependencies {

        sub.plugins.withId('kotlin') {
            compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        }

        testImplementation("org.junit.jupiter:junit-jupiter-api:${junit_version}")
        testImplementation("org.junit.jupiter:junit-jupiter-params:${junit_version}")
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junit_version}")
		
    }

    test {
        useJUnitPlatform()
    }
    
    sub.plugins.withId('kotlin') {
        compileKotlin {
            kotlinOptions {
                jvmTarget = "1.8"
                javaParameters = true
                
                //required for @JvmDefault
                freeCompilerArgs = ['-Xjvm-default=enable']
            }
        }

        compileTestKotlin {
            kotlinOptions {
                jvmTarget = "1.8"
                javaParameters = true
            }
        }
    }

}

