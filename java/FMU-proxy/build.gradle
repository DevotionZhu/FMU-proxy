
plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.11" apply false //apply in sub-projects
}

println "Gradle version is ${gradle.getGradleVersion()}"

ext.fmi4j_snapshot = false
ext.fmi4j_version = fmi4j_snapshot ? 'dev-SNAPSHOT' : '0.13.1'

ext.grpc_version = '1.17.1'
ext.yajrpc_version = 'v0.10.2'
ext.slf4j_version = '1.7.25'

subprojects { sub ->

    apply plugin: 'java'

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    dependencies {

        sub.plugins.withId('kotlin') {
            implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
            implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: '1.3.11'
        }

        def junit_version = '5.3.2'
        testImplementation("org.junit.jupiter:junit-jupiter-api:${junit_version}")
        testImplementation("org.junit.jupiter:junit-jupiter-params:${junit_version}")
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junit_version}")
		
    }

    test {
        useJUnitPlatform()
    }
    
    sub.plugins.withId('kotlin') {
        compileKotlin {
            kotlinOptions {
                jvmTarget = "1.8"
                javaParameters = true
                
                //required for @JvmDefault
                freeCompilerArgs = ['-Xjvm-default=enable']
            }
        }

        compileTestKotlin {
            kotlinOptions {
                jvmTarget = "1.8"
                javaParameters = true
            }
        }
    }

}

