
class PublishPluginExtension {
    String description = ''
    String group = 'no.ntnu.ihb.fmuproxy'
}

class PublishPlugin implements Plugin<Project>{

    protected Jar sourcesJar(Project project) {
        Jar task = project.tasks.create("sourcesJar", Jar.class)
        task.from project.sourceSets.main.allSource
        task.archiveClassifier = 'sources'
        return task
    }

    protected Jar javadocJar(Project project) {
        Jar task = project.tasks.create("javadocJar", Jar.class)
        task.from project.javadoc.destinationDir
        task.archiveClassifier = 'javadoc'
        return task
    }

    void apply(Project project) {

        def extension = project.extensions
                .create('pomDescription', PublishPluginExtension)

        project.apply(plugin: MavenPublishPlugin)

        
        project.version = 'dev-SNAPSHOT'

        println("maven version for ${project.archivesBaseName} is: ${project.version}")

        
        project.publishing {

            project.group = extension.group
            
            publications {
                mavenJava(MavenPublication) {

                    from project.components.java
                    artifact sourcesJar(project)
                    artifact javadocJar(project)

                    pom {
                        name = project.archivesBaseName
                        description = extension.description
                        url = 'https://github.com/NTNU-IHB/FMU-proxy'

                        scm {
                            connection = 'scm:git:git://github.com/NTNU-IHB/FMU-proxy.git'
                            developerConnection = 'scm:git:ssh://github.com/NTNU-IHB/FMU-proxy.git'
                            url = 'https://github.com/NTNU-IHB/FMU-proxy/tree/master'
                        }

                        licenses {
                            license {
                                name = 'The MIT License'
                                url = 'https://opensource.org/licenses/mit-license.php'
                            }
                        }

                        developers {
                            developer {
                                id = 'laht'
                                name = 'Lars Ivar Hatledal'
                                email = 'laht@ntnu.no'
                            }
                        }
                    }
                }
            }
        }
        
        if (project.hasProperty("ossrhUsername") && project.hasProperty("ossrhPassword")) {

            project.apply(plugin: SigningPlugin)

            project.publishing {
                repositories {
                    maven {
                        def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                        def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
                        url project.version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                        credentials {
                            username = project.ossrhUsername
                            password = project.ossrhPassword
                        }
                    }
                }
            }

            project.signing {
                sign project.publishing.publications.mavenJava
            }

        }
    }

}

apply plugin: PublishPlugin