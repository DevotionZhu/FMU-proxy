

apply plugin: 'kotlin'
apply plugin: 'war'
apply from: 'https://raw.github.com/akhikhl/gretty/master/pluginScripts/gretty.plugin'


def snapshot = true
def fmi4j_version = snapshot ? fmi4j_snapshot_version : fmi4j_stable_version

dependencies {

    providedCompile 'javax.servlet:servlet-api:2.2'
    providedCompile 'javax.servlet.jsp:jsp-api:2.2'
    providedCompile 'javax.enterprise:cdi-api:2.0'

    compile group: 'com.sun.faces', name: 'jsf-api', version: '2.2.15'
    runtime group: 'com.sun.faces', name: 'jsf-impl', version: '2.2.15'

    compile group: 'org.primefaces', name: 'primefaces', version: '6.1'

    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'
    compile group: 'org.zeromq', name: 'jeromq', version: '0.4.3'

    compile group: 'no.mechatronics.sfi.fmi4j', name: 'fmi-modeldescription', version: fmi4j_version, changing: snapshot

}

gretty {
    servletContainer = "jetty9.3"
    httpEnabled = true
    httpPort = 8080
    contextPath = "/fmu-proxy"
    logDir = "$projectDir/logs"
}

task copyThrift(type: Copy) {

    from('../gen-thrift-src/src/main/thrift')
    into('src/main/webapp/resources/thrift')
    include('*.thrift')

}

task copyProto(type: Copy) {

    from('../gen-proto-src/src/main/proto')
    into('src/main/webapp/resources/proto')
    include('*.proto')

}

task copyProtoZip(type: Zip) {

    from '../gen-proto-src/src/main/proto'
    include '*'
    archiveName 'fmu-proxy-generic-proto.zip'
    destinationDir(file('src/main/webapp/resources/proto/'))
}

compileKotlin.dependsOn(copyThrift)
compileKotlin.dependsOn(copyProto)
compileKotlin.dependsOn(copyProtoZip)
