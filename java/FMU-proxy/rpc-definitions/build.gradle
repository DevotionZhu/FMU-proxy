
plugins {
    id 'kotlin'
    id 'java-library'
    id "com.google.protobuf" version '0.8.8'
}

    
dependencies {

    api group: 'io.grpc', name: 'grpc-stub', version: grpc_version
    api group: 'io.grpc', name: 'grpc-protobuf', version: grpc_version

    api group: 'org.apache.thrift', name: 'libthrift', version: '0.12.0'

    api group: 'com.github.NTNU-IHB.FMI4j', name: 'fmi-import', version: fmi4j_version, changing: fmi4j_snapshot

}

if (!getProperties().get("disableCodeGeneration", false)) {
    
    def definitions = "../../../rpc-definitions"

    tasks.create('generateThrift') {

        doLast {
            
            def out = new File("src/main/java")
            if (!out.exists()) {
                out.mkdirs()
            }

            try {

                exec {
                    executable 'thrift'
                    args '-r', '-out', 'src/main/java', '--gen', 'java:private-members,fullcamel', "$definitions/thrift/service.thrift"
                }

            } catch (Exception ex) {
                println ex
            }
            
        }

    }

    protobuf {
        generatedFilesBaseDir = "$projectDir/src"
        protoc {
            artifact = 'com.google.protobuf:protoc:3.6.1'
        }
        plugins {
            grpc {
                artifact = "io.grpc:protoc-gen-grpc-java:${grpc_version}"
            }
        }
        generateProtoTasks {
            ofSourceSet('main')*.plugins {
                grpc {
                    outputSubDir = 'java'
                }
            }
        }

    }

    sourceSets {
        main {
            proto {
                srcDir "$definitions/proto"
            }
        }
    }

    tasks.create('generateAll') {
        dependsOn 'generateProto', 'generateThrift'
    }

}
