
buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.org.jruyi.gradle:thrift-gradle-plugin:0.4.0"
    }
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.5'
    }
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.commercehub.gradle.plugin:gradle-avro-plugin:0.14.0"
    }
}

apply plugin: "com.commercehub.gradle.plugin.avro"
apply plugin: 'com.google.protobuf'
apply plugin: "org.jruyi.thrift"

apply plugin: 'maven'
apply plugin: 'signing'

group = 'no.mechatronics.sfi.fmu_proxy'
version = '0.1-SNAPSHOT'

dependencies {
    compile group: 'org.apache.thrift', name: 'libthrift', version: '0.11.0'

    compile group: 'io.grpc', name: 'grpc-stub', version: '1.11.0'
    compile group: 'io.grpc', name: 'grpc-protobuf', version: '1.11.0'

    compile group: 'org.apache.avro', name: 'avro', version: '1.8.2'
    compile group: 'org.apache.avro', name: 'avro-ipc', version: '1.8.2'
}

avro {
    fieldVisibility = "PRIVATE"
}

protobuf {
    generatedFilesBaseDir = "$projectDir/build"
    protoc {
        artifact = 'com.google.protobuf:protoc:3.5.1'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.11.0'
        }
    }
    generateProtoTasks {

        ofSourceSet('main')*.plugins {
            grpc {
                outputSubDir = 'java'
            }
        }
    }

}

compileThrift {
    thriftExecutable "$projectDir/src/main/thrift/thrift-0.11.0.exe"
    createGenFolder false
    generator 'java', 'private-members'
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name archivesBaseName
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Generated thrift sources for use by FMU-proxy'
                url 'https://github.com/SFI-Mechatronics/gRPC-FMU'

                scm {
                    connection 'scm:git:git://github.com/SFI-Mechatronics/gRPC-FMU.git'
                    developerConnection 'scm:git:ssh://github.com/SFI-Mechatronics/gRPC-FMU.git'
                    url 'https://github.com/SFI-Mechatronics/gRPC-FMU/tree/master'
                }

                licenses {
                    license {
                        name 'The MIT License'
                        url 'https://opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id 'laht'
                        name 'Lars Ivar Hatledal'
                        email 'laht@ntnu.no'
                    }
                }
            }
        }
    }
}
