
buildscript {
    ext.kotlin_version = '1.2.21'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.4'
    }
}

apply plugin: 'com.google.protobuf'
apply plugin: 'kotlin'

def snapshot = false
def fmi_import_version = snapshot ? '0.2.7-SNAPSHOT' : '0.2.6'

wrapper {
    gradleVersion = '4.5'
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/" //fmi-import
    }
}

dependencies {

    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    compile group: 'org.zeromq', name: 'jeromq', version: '0.4.3'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'

    compile group: 'io.grpc', name: 'grpc-stub', version: '1.9.0'
    compile group: 'io.grpc', name: 'grpc-netty', version: '1.9.0'
    compile group: 'io.grpc', name: 'grpc-protobuf', version: '1.9.0'

    compile group: 'com.google.protobuf', name: 'protobuf-java', version: '3.5.1'
    compile group: 'com.google.protobuf', name: 'protobuf-java-util', version: '3.5.1'

    compile group: 'no.mechatronics.sfi.fmi4j', name: 'fmi-import', version: fmi_import_version, changing: snapshot //central
    compile group: 'no.mechatronics.sfi.grpc_fmu', name: 'generic-server', version: '0.1-SNAPSHOT', changing: true

    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.25'

}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

protobuf {
    generatedFilesBaseDir = "$projectDir/src"
    protoc {
        artifact = 'com.google.protobuf:protoc:3.5.1'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.9.0'
        }
    }
    generateProtoTasks {

        ofSourceSet('main')*.plugins {
            grpc {
                outputSubDir = 'java'
            }
        }
    }

}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': project.name,
        'Implementation-Version': "1.0",
        'Main-Class': 'no.mechatronics.sfi.rmu.Main'
    }
    baseName = project.name
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    with jar
}
