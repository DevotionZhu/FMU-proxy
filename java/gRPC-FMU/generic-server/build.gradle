buildscript {
    ext.kotlin_version = '1.2.21'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.4'
    }
}

apply plugin: 'com.google.protobuf'
apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'no.mechatronics.sfi.grpc_fmu'
version = '0.1-SNAPSHOT'

repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/" //fmi-import
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    compile group: 'org.zeromq', name: 'jeromq', version: '0.4.3'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'

    compile group: 'io.grpc', name: 'grpc-stub', version: '1.9.0'
    compile group: 'io.grpc', name: 'grpc-netty', version: '1.9.0'
    compile group: 'io.grpc', name: 'grpc-protobuf', version: '1.9.0'

    compile group: 'com.google.protobuf', name: 'protobuf-java', version: '3.5.1'
    compile group: 'com.google.protobuf', name: 'protobuf-java-util', version: '3.5.1'

    compile group: 'info.laht', name: 'yaj-rpc', version: '0.2-SNAPSHOT'
    compile group: 'no.mechatronics.sfi.fmi4j', name: 'fmi-import', version: '0.2.8-SNAPSHOT'

    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    runtime group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.25'

}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

protobuf {
    generatedFilesBaseDir = "$projectDir/src"
    protoc {
        artifact = 'com.google.protobuf:protoc:3.5.1'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.9.0'
        }
    }
    generateProtoTasks {

        ofSourceSet('main')*.plugins {
            grpc {
                outputSubDir = 'java'
            }
        }
    }

}

sourceSets {
    main {
        proto {
            srcDir '../../../proto'
        }
    }
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name archivesBaseName
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'A Java server, written in Kotlin, for exposing FMUs over the network using gRPC '
                url 'https://github.com/SFI-Mechatronics/gRPC-FMU'

                scm {
                    connection 'scm:git:git://github.com/SFI-Mechatronics/gRPC-FMU.git'
                    developerConnection 'scm:git:ssh://github.com/SFI-Mechatronics/gRPC-FMU.git'
                    url 'https://github.com/SFI-Mechatronics/gRPC-FMU/tree/master'
                }

                licenses {
                    license {
                        name 'The MIT License'
                        url 'https://opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id 'laht'
                        name 'Lars Ivar Hatledal'
                        email 'laht@ntnu.no'
                    }
                }
            }
        }
    }
}